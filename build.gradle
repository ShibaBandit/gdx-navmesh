plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group 'io.github.shibabandit'
version '0.3.0'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
    mavenLocal()
}

ext {
    gdxVersion = '1.9.6'
    aiVersion = '1.8.1'
    isReleaseVersion = !version.endsWith("SNAPSHOT")
}

dependencies {
    compile "com.badlogicgames.gdx:gdx:$gdxVersion"
    compile "com.badlogicgames.gdx:gdx-ai:$aiVersion"
    compile 'org.locationtech.jts:jts-core:1.15.1'
    compile 'org.orbisgis:poly2tri-core:0.1.2'

    testCompile "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion" // Should be for testing visually only
    testCompile "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop" // Should be for testing visually only
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'gdx-navmesh'
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'gdx-navmesh'
                description = 'LibGDX navigation mesh using constrained Delaunay triangulation.'
                url = 'https://github.com/ShibaBandit/gdx-navmesh'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'ShibaBandit'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/ShibaBandit/gdx-navmesh'
                    developerConnection = 'scm:git:ssh://github.com:ShibaBandit/gdx-navmesh'
                    url = 'https://github.com/ShibaBandit/gdx-navmesh'
                }
            }
        }
    }
    repositories {
        maven {
            // change URLs to point to your repos, e.g. http://my.org/repo
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "$buildDir/repos/snapshots"
            url = isReleaseVersion ? releasesRepoUrl : snapshotsRepoUrl
            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }
}

signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }
    sign publishing.publications.mavenJava
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html4', true)
    }
}